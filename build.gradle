buildscript {
  ext.versions = [
      'minSdk': 16,
      'compileSdk': 29,
      'buildTools': '29.0.2',

      'kotlin': '1.3.61',
      'errorProne': '2.0.19',
  ]

  ext.deps = [
      'gson': 'com.google.code.gson:gson:2.8.2',
      'argparser': 'com.xenomachina:kotlin-argparser:2.0.7',
      'commonsLang3': 'org.apache.commons:commons-lang3:3.3.1',
      'commonsIo': 'commons-io:commons-io:2.5',
      'ddmlib': 'com.android.tools.ddms:ddmlib:26.5.3',
      'animatedGifLib': 'com.madgag:animated-gif-lib:1.2',
      'guava': 'com.google.guava:guava:21.0',
      'lesscss': 'org.lesscss:lesscss:1.3.3',
      'mustache': 'com.github.spullara.mustache.java:compiler:0.8.14',
      'jacocoMavenPlugin': 'org.jacoco:jacoco-maven-plugin:0.8.5',
      'butterknife': [
          'runtime': 'com.jakewharton:butterknife:10.2.0',
          'compiler': 'com.jakewharton:butterknife-compiler:10.2.0'
      ],
      'kotlinStdLibJdk8': "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}",

      'junit': 'junit:junit:4.12',
      'truth': 'com.google.truth:truth:1.0',

      'support': [
          'appCompat': "androidx.appcompat:appcompat:1.1.0",
          'constraintLayout': "androidx.constraintlayout:constraintlayout:1.1.3",

          'test': [
              'espresso': 'androidx.test.espresso:espresso-core:3.2.0',
              'espressoContrib': 'androidx.test.espresso:espresso-contrib:3.2.0',
              'runner': 'androidx.test:runner:1.2.0',
          ],
      ],
  ]

  dependencies {
    classpath 'com.android.tools.build:gradle:3.5.3'
    classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
    classpath 'net.ltgt.gradle:gradle-errorprone-plugin:1.1.1'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    classpath 'digital.wup:android-maven-publish:3.6.3'
  }

  repositories {
    google()
    jcenter()
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }
}

subprojects { project ->
  group = GROUP
  version = VERSION_NAME

  repositories {
    google()
    jcenter()
  }

//  apply plugin: 'checkstyle'
//  project.checkstyle {
//    toolVersion "7.6.1"
//    configFile rootProject.file('checkstyle.xml')
//  }

//  if (!project.path.startsWith(':third-party')) {
//    project.apply(plugin: 'net.ltgt.errorprone')
//  }

  configurations.all {
    resolutionStrategy {
      eachDependency { details ->
        // Force all of the primary support libraries to use the same version.
        if (details.requested.group == 'com.android.support') {
          details.useVersion versions.supportLibrary
        }
        // Force all the error-prone dependencies to use the same version.
        if (details.requested.group == 'com.google.errorprone'
            && details.requested.name.startsWith('error_prone_')) {
          details.useVersion versions.errorProne
        }
      }
    }
  }
}
